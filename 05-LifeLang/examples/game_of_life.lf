const FIELD_WIDTH  = 64;
const FIELD_HEIGHT = 64;
const CELL_SIZE    = 4;

const DEAD   = 0;
const ALIVE1 = 1;
const ALIVE2 = 2;

const MS_PER_GENERATION = 10;
const ALIVE_PROB = 30;

const ALIVE_COLOR1 = 0xC71585;
const ALIVE_COLOR2 = 0x00FF00;
const DEAD_COLOR   = 0x000000;

func app() : void {
  var current[FIELD_HEIGHT][FIELD_WIDTH];
  var next[FIELD_HEIGHT][FIELD_WIDTH];

  var y = 0;
  var x = 0;
  var last_flush_time = ticks();

  for (y = 0; y < FIELD_HEIGHT; y = y + 1) {
    for (x = 0; x < FIELD_WIDTH; x = x + 1) {
      if (rand() % 100 < ALIVE_PROB) {
        if (rand() % 2 == 1) { current[y][x] = ALIVE2; }
        else { current[y][x] = ALIVE1; }
      }
    }
  }

  while (finish() == 0) {
    for (y = 0; y < FIELD_HEIGHT; y = y + 1) {
      for (x = 0; x < FIELD_WIDTH; x = x + 1) {
        var cell = current[y][x];
        var color = DEAD_COLOR;
        if (cell == ALIVE1) { color = ALIVE_COLOR1; }
        else {
          if (cell == ALIVE2) { color = ALIVE_COLOR2; }
        }
        fill_rect(x * CELL_SIZE, y * CELL_SIZE, CELL_SIZE, CELL_SIZE, color);
      }
    }

    var current_time = ticks();
    var elapsed = current_time - last_flush_time;
    if (elapsed < MS_PER_GENERATION) {
      delay(MS_PER_GENERATION - elapsed);
    }
    flush();
    last_flush_time = ticks();

    for (y = 0; y < FIELD_HEIGHT; y = y + 1) {
      for (x = 0; x < FIELD_WIDTH; x = x + 1) {
        var neighbors = 0;
        var count1 = 0;
        var count2 = 0;

        var dy = -1;
        for (dy = -1; dy <= 1; dy = dy + 1) {
          var dx = -1;
          for (dx = -1; dx <= 1; dx = dx + 1) {
            if (dy == 0 && dx == 0) { continue; }

            var ny = (y + dy + FIELD_HEIGHT) % FIELD_HEIGHT;
            var nx = (x + dx + FIELD_WIDTH) % FIELD_WIDTH;

            if (current[ny][nx] == ALIVE1) { neighbors = neighbors + 1; count1 = count1 + 1; }
            else {
              if (current[ny][nx] == ALIVE2) { neighbors = neighbors + 1; count2 = count2 + 1; }
            }
          }
        }

        if (current[y][x] > DEAD) {
          if (neighbors == 2 || neighbors == 3) { next[y][x] = current[y][x]; }
          else { next[y][x] = DEAD; }
        } else {
          if (neighbors == 3) {
            if (count1 > count2) { next[y][x] = ALIVE1; }
            else { next[y][x] = ALIVE2; }
          } else {
            next[y][x] = DEAD;
          }
        }
      }
    }

    for (y = 0; y < FIELD_HEIGHT; y = y + 1) {
      for (x = 0; x < FIELD_WIDTH; x = x + 1) {
        current[y][x] = next[y][x];
      }
    }
  }
}
